plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.graalvm.buildtools.native' version '0.10.5'
}

group = 'net.shamansoft'
version = '0.3.7'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

springBoot {
	buildInfo()   // This will generate a build-info.properties file with accurate values
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation('com.google.genai:google-genai:0.1.0') {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
	implementation 'com.google.apis:google-api-services-drive:v3-rev20250511-2.0.0'
	implementation 'org.jsoup:jsoup:1.15.3'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
	archiveFileName = 'cookbook.jar'
}

test {
	useJUnitPlatform()
}

graalvmNative {
	binaries {
		main {
			imageName = 'cookbook'
			buildArgs.addAll(
					'--no-fallback',
					'--initialize-at-run-time=org.apache.commons.logging.LogFactory,org.apache.commons.logging.LogFactoryService',
					'--initialize-at-run-time=ch.qos.logback',
					'-H:+ReportExceptionStackTraces'
			)
			resources.autodetect()
		}
	}
}
